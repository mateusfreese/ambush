plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'androidx.navigation.safeargs.kotlin'
}

android {
    compileSdkVersion 30
    buildToolsVersion '30.0.3'

    defaultConfig {
        applicationId "com.ambush.ambushchallenge"
        minSdkVersion 21
        targetSdkVersion 30
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        debug {
            debuggable = true
            buildConfigField "String", "BASE_URL", "\"https://api.github.com/\""
        }

        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            buildConfigField "String", "BASE_URL", "\"https://api.github.com/\""
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
    buildFeatures {
        viewBinding true
    }
}

configurations {
    ktlint
}

dependencies {
    def dependencies = rootProject.ext.dependencies

    implementation dependencies.kotlin
    implementation dependencies.androixCore
    implementation dependencies.appCompat
    implementation dependencies.material
    implementation dependencies.constraintLayout
    implementation dependencies.lifecycleLivedata
    implementation dependencies.lifecycleViewmodel
    implementation dependencies.coroutines
    implementation dependencies.koin
    implementation dependencies.retrofit
    implementation dependencies.httpInterceptor
    implementation dependencies.navigationFragment
    implementation dependencies.navigationUi
    implementation dependencies.moshi
    implementation dependencies.paper

    ktlint dependencies.ktlint

    testImplementation dependencies.junit
    testImplementation dependencies.mockk
    testImplementation dependencies.coreTesting
    testImplementation dependencies.coroutinesTest
    androidTestImplementation dependencies.androidxJunit
}

task ktlint(type: JavaExec, group: "verification") {
    description = "Check Kotlin code style."
    main = "com.pinterest.ktlint.Main"
    classpath = configurations.ktlint
    args "src/**/*.kt"
}
check.dependsOn ktlint

task ktlintFormat(type: JavaExec, group: "formatting") {
    description = "Fix Kotlin code style deviations."
    main = "com.pinterest.ktlint.Main"
    classpath = configurations.ktlint
    args "-F", "src/**/*.kt"
}